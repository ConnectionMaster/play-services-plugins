apply plugin: 'maven-publish'

jar {
    from 'LICENSE'
}

groovydoc {
    exclude     "**/internal/**"
    includePrivate false

    docTitle project.ext.pomDesc
    header ""
    footer "Copyright (C) 2015 The Android Open Source Project"
}

task javadocJar(type: Jar, dependsOn:groovydoc) {
    classifier  'javadoc'
    from        groovydoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId project.ext.archivesBaseName

            artifact sourcesJar
            artifact javadocJar
            
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
}

publishing.repositories.maven {
    url "$buildDir/repo"
}
